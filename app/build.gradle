buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'com.jakewharton.hugo:hugo-plugin:1.2.1'
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'com.jakewharton.hugo'

repositories {
    mavenCentral() // For Glide
    maven { url 'https://maven.fabric.io/public' }
    google()
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "net.feheren_fekete.applist"
        minSdkVersion 21
        targetSdkVersion 28

        versionCode 48
        versionName "4.8"

        multiDexEnabled true

        // Set this to false. Otherwise vector drawables won't work everywhere:
        // https://stackoverflow.com/questions/35761636/is-it-possible-to-use-vectordrawable-in-buttons-and-textviews-using-androiddraw/41855991#41855991
        vectorDrawables.useSupportLibrary = false
    }
    buildToolsVersion '28.0.3'
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    // TODO: Remove when this is fixed: https://github.com/Kotlin/kotlinx.coroutines/issues/1064
    packagingOptions {
        pickFirst("META-INF/atomicfu.kotlin_module")
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.2.0-alpha-2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.2.0-alpha-2'

    // Koin (dependency injection)
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-java:$koin_version" // For using Koin from Java code
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-scope:$koin_version"

    // Android
    kapt 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha04'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'
    implementation 'androidx.percentlayout:percentlayout:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'com.google.android.material:material:1.1.0-alpha05'

    // Crashlytics
    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }

    // Firebase
    implementation 'com.google.firebase:firebase-core:16.0.8'

    // Material dialog
    // We use this because this does a good job of not-covering
    // the dialog when the software keyboard is active.
    implementation 'com.afollestad.material-dialogs:core:2.7.0'
    implementation 'com.afollestad.material-dialogs:input:2.7.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    kapt 'com.github.bumptech.glide:compiler:4.8.0'

    // EventBus
    implementation 'org.greenrobot:eventbus:3.1.1'
    kapt 'org.greenrobot:eventbus-annotation-processor:3.1.1'

    // Others
    implementation 'com.parse.bolts:bolts-tasks:1.4.0'
}

kapt {
    arguments {
        arg('eventBusIndex', 'net.feheren_fekete.applist.ApplistEventBusIndex')
    }
}

// Must be last
apply plugin: 'com.google.gms.google-services'